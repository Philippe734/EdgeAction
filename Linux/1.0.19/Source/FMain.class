' Gambas class file

'
' Edge Action
' Execute command when right clic on edge of screen for Linux.
' You can set upt 4 commands.
' Select preferences in the tray icon to modify commands.
' Copyright @ Philippe734
' https://github.com/Philippe734/EdgeAction
' 2020.03
'

Public tt As New MouseIO As "ett"
Private bFlag As Boolean ' clic on edge
Private bClicNormal As Boolean ' disable/enable clic
Private iMouseID As Integer ' with xinput
Public sSettingsFile As String ' path to settings file
Public sCommandTopEdge As String
Public sCommandLeftEdge As String
Public sCommandRightEdge As String
Public sCommandBottomEdge As String

Public Sub Form_Open()
  
  Print "Begin", Timer    
  
  Me.Height = 2
  Me.Width = 2
  
  bClicNormal = True
  
  menuTray.Visible = False
  TrayIcon1.PopupMenu = menuTray.Name
  sSettingsFile = User.Home & "/.config/gambas3/" & Application.Name & ".conf"
  
  LoadSettings
  
  WriteSettings
  
  GetMouseID ' with xinput
  
  tt.Start() ' start timer to hook mouse position
  
End

Public Sub ett_MouseMove(X As Integer, Y As Integer)
  
  Dim sClic As String
  Dim sShell As String
  Dim sEdge As String
  
  'Hide the main form
  Me.Visible = False
  
  If X > (Screen.Width - 3) And If sCommandRightEdge <> "" Then
    sEdge = "Right"
    'Print "right edge", Timer
  Else If Y < 3 And If sCommandTopEdge <> "" Then
    sEdge = "Top"
    'Print "Top edge", Timer
  Else If X < 3 And If sCommandLeftEdge <> "" Then
    sedge = "Left"
    'Print "Left edge", Timer
  Else If Y > (Screen.Height - 3) And If sCommandBottomEdge <> "" Then
    sEdge = "Bottom"
    'Print "Bottom edge", Timer
  Else ' whole screen except edges
    bFlag = 0
    EnableB3
    sEdge = "False"
  Endif
  
  If sEdge <> "False" Then
    DisableB3 ' disable the mouse right clic
    sShell = "xinput --query-state " & iMouseID & " | grep 'button" & Chr(92) & "[3" & Chr(92) & "]=down' | cut -d'=' -f 2"
    Shell sShell Wait To sClic
    sClic = Trim(sClic)
    If sClic = "down" And bFlag = 0 Then
      bFlag = 1
      Select Case sEdge
        Case "Top"
          Shell sCommandTopEdge
          'Print "sCommandTopEdge", Timer
        Case "Left"
          Shell sCommandLeftEdge
          'Print "sCommandLeftEdge", Timer
        Case "Right"
          Shell sCommandRightEdge
          'Print "sCommandRightEdge", Timer
        Case "Bottom"
          Shell sCommandBottomEdge
          'Print "sCommandBottomEdge", Timer
      End Select
    Endif 
  Endif  
  
End

Private Sub DisableB3() ' disable the right clic
  
  If bClicNormal = True Then
    bClicNormal = False
    Shell "xinput set-button-map " & iMouseID & " 1 2 0" Wait
    'Print "clic disabled", Timer
  Endif
  
End

Private Sub EnableB3() ' enable the right clic
  
  If bClicNormal = False Then
    bClicNormal = True
    Shell "xinput set-button-map " & iMouseID & " 1 2 3" Wait
    'Print "clic enabled", Timer
  Endif
  
End

Private Sub GetMouseID()
  
  Dim sShell As String, sID As String
  
  sShell = "xinput --list | grep -i -m 1 'mouse' | grep -o 'id=[0-9]" & Chr(92) & "+' | grep -o '[0-9]" & Chr(92) & "+'"
  Shell sShell Wait To sID
  iMouseID = CInt(Trim(sID))
  'Print iMouseID, Timer
  
End

Public Sub menuTrayQuit_Click()
  
  Me.Close
  
End

Public Sub Form_Close()
  
  TrayIcon1.Delete
  EnableB3
  WriteSettings
  Print "End", Timer
  
End

Public Sub menuTrayPrefs_Click()  
  
  FText.ShowModal
  LoadSettings
  
End

Private Sub WriteSettings() 
  
  Settings["Command/Exist"] = "True"
  Settings["Command/TopEdge"] = sCommandTopEdge
  Settings["Command/LeftEdge"] = sCommandLeftEdge
  Settings["Command/RightEdge"] = sCommandRightEdge
  Settings["Command/BottomEdge"] = sCommandBottomEdge
  
  If Not Exist(sSettingsFile) Then
    Settings.Save    
  Endif
  
  'Print "settings written", Timer
  
End

Private Sub LoadSettings()
  
  Settings.Reload
  sCommandTopEdge = Settings["Command/TopEdge", Null]
  sCommandLeftEdge = Settings["Command/LeftEdge", Null]
  sCommandRightEdge = Settings["Command/RightEdge", Null]
  sCommandBottomEdge = Settings["Command/BottomEdge", Null]
  
  'Print "settings loaded", Timer
  
End

Public Sub menuTrayAbout_Click()
  
  FAbout.Show
  
End
